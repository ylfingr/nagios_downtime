#!/usr/bin/perl -w
# ##############################################################################
# nagios_downtime
#
# Copyright (c) 2007-2012 Lars Michelsen <lm@larsmichelsen.com>
# http://larsmichelsen.com
#
# Permission is hereby granted, free of charge, to any person
# obtaining a copy of this software and associated documentation
# files (the "Software"), to deal in the Software without
# restriction, including without limitation the rights to use,
# copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following
# conditions:
#
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
# OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
# HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
# WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
# OTHER DEALINGS IN THE SOFTWARE.
# ##############################################################################
# SCRIPT:       nagios_downtime
# AUTHOR:       Lars Michelsen <lars@vertical-visions.de>
# DECRIPTION:   Sends a HTTP(S)-GET to the nagios web server to
#                  enter a downtime for a host or service.
# CHANGES:
# 2008-05-19 v0.4   - Some code formating
#                   - The downtime type is now automaticaly detected on given params
#                   - Changed case of the parameters
#                   - Added proxy configuration options
#                   - User Agent is now "nagios_downtime.pl / <version>"
#                   - Added parameter -S and -p for setting server options via param
# 2009-10-13 v0.5   - Added new "run modes": add (default) and del to schedule
#                     or remove downtimes
#                   - Using the del mode the last scheduled downtime will be removed
#                   - Added the params -u and -P for giving the username and password
#                     for accessing the CGIs by the command line
#                   - Several code cleanups
# 2009-11-30 v0.5.1 - Fixed service downtime deletion
# 2010-04-05 v0.6   - Chomping newlines from stored downtimes now
# 2010-06-20 v0.6.1 - Fixed env_proxy calls to LWP object
#                   - Explicitly unsetting env_proxy when no proxy set
#                   - Checking for Crypt::SSLeay module to load when https
#                     url given
# 2012-12-04 v0.7   - Implemented support for setting downtimes via
#                     JSON API of the Check_MK Multisite GUI
#                     (only adding downtimes has been tested here, sorry)
#                   - Restructured configuration parameters
#                   - Added optional configuration nagios_downtime.conf
#                     (no need to change this script anymore)
#                   - LWP instance supports cookie handling now
#                   - Code cleanups
# 2013-07-31 v0.8   - Deleting of downtimes using multisite works now
#                     (Needs Check_MK >= 1.2.3i2)
# 2016-11-08 v0.9   - Enable setting downtimes for Wato Folder(s) and Service regexp
#
# ##############################################################################

# ##############################################################################
# Configuration (-> Here you have to set some values!)
# ##############################################################################

# Interface/API to use to set downtimes. Can be set to "nagios" to use the
# default CGI based webinterface or "multisite" to set downtimes via Check_MK
# Multisite
my $type = 'multisite';

# Protocol for the GET Request, In most cases "http", "https" is also possible
my $webProto = "http";
# IP or FQDN of Nagios server (example: nagios.domain.de)
my $server = "localhost
# IP or FQDN of Nagios web server. In most cases same as $server, if
# empty automaticaly using $server
my $webServer = "";
# Port of Nagios webserver (If $webProto is set to https, this should be
# SSL Port 443)
my $webPort = 80;
# Web path to Nagios cgi-bin (example: /nagios/cgi-bin) (NO trailing slash!)
# Or if type is multisite, path to multisite (e.g. /check_mk)
my $basePath = "/cmk/check_mk";

# When your interface is secured by basic authentication, you need to specify
# the following three options. If you want to disable the basic authentication
# support, just leave the password or the authName empty.
#
# User to take for authentication and author to enter the downtime (example:
# nagiosadmin)
my $user = "nagiosadmin";
# Password for above user. In case of multisite API you need the auth secret
# of the user.
my $userPw = "";
# Name of authentication realm, set in the Nagios .htaccess file 
# (example: "Nagios Access")
my $authName = "Nagios Access";

#
# Nagios CGI specific options
# (only needed when $type is set to "nagios")
#

# Nagios date format (same like set in value "date_format" in nagios.cfg)
my $nagiosDateFormat = "strict-iso8601";

# When you have to use a proxy server for access to the nagios server, set the
# URL here. The proxy will be set for this script for the choosen web protocol
# When this is set to 'env', the proxy settings will be read from the env.
my $proxyAddress = '';

# Enable fetching and storing the downtime ids for later downtime removal
# The downtime IDs will be stored in a defined temp directory
my $storeDowntimeIds = 1;
# The script will generate temporary files named (<host>.txt or 
# <host>-<service>.txt). The files will contain the script internal
# downtime ids and/or the nagios downtime ids.
# These files are needed for later downtime removal
my $downtimePath = '/tmp';

# Some default options (Usualy no changes needed below this)

use strict;
use Time::HiRes qw(clock_gettime CLOCK_REALTIME);

use constant {
    HOST_DOWNTIME      => 1,
    SERVICE_DOWNTIME   => 2,
    FOLDER_DOWNTIME    => 3,
    SVCREGEXP_DOWNTIME => 4,
};
# Script internal downtime id for a new downtime
# Using the current timestamp as script internal downtime identifier
my $downtimeId = clock_gettime(CLOCK_REALTIME);
# Default downtime type (1: Host Downtime, 2: Service Downtime, 3: Folder(-based) Downtime)
my $downtimeType = HOST_DOWNTIME;
# Default Downtime duration in minutes
my $downtimeDuration = 0;
# Default Downtime text
my $downtimeComment = "Perl Downtime-Script";
# Default Debugmode: off => 0 or on => 1
my $debug = 0;
# Script version
my $version = "0.8";

# Timezone
#my $timezone = 'Europe/Berlin';



# ##############################################################################
# Don't change anything below, except you know what you are doing.
# ##############################################################################

use strict;
use warnings;
use Net::Ping;
use LWP 5.64;
use Sys::Hostname;
use Getopt::Long;
use Switch;
use DateTime;
use URI::Encode;
use JSON qw(decode_json);
use Data::Dumper::Again;
my $dd = Data::Dumper::Again->new(purity => 1, terse => 1, deepcopy => 1, quotekeys => 1, sortkeys => 1, deparse => 1);

my $arg;
my $p;
my $i = 0;
my $oBrowser;
my $oResponse;
my $hostname = "";
my $service = "";
my $folder = "";
my $svcregexp = "";
my $start = "";
my $end = "";
my $start_date;
my $start_time;
my $end_date;
my $end_time;
my $duration;
my $url = "";
my $help = "";
my $now = time;
my $timezone = "local";
my $mode = "add";

my %urls = (
    nagios => {
        host_downtime        => "[baseUrl]/cmd.cgi?cmd_typ=55&cmd_mod=2"
    ."&host=[hostname]&com_author=[user]&com_data=[comment]"
    ."&trigger=0&start_time=[start_time]&end_time=[end_time]"
    ."&fixed=1&childoptions=1&btnSubmit=Commit",
        service_downtime     => "[baseUrl]/cmd.cgi?cmd_typ=56&cmd_mod=2"
    ."&host=[hostname]&service=[service]"
    ."&com_author=[user]&com_data=[comment]"
    ."&trigger=0&start_time=[start_time]&end_time=[end_time]"
    ."&fixed=1&btnSubmit=Commit",
    del_host_downtime    => "[baseUrl]/cmd.cgi?cmd_typ=78&cmd_mod=2&down_id=[downtime_id]&btnSubmit=Commit",
    del_service_downtime => "[baseUrl]/cmd.cgi?cmd_typ=79&cmd_mod=2&down_id=[downtime_id]&btnSubmit=Commit",
    all_downtimes        => "[baseUrl]/extinfo.cgi?type=6",
},
    multisite => {
        folder_downtime        => "[baseUrl]/view.py?filled_in=actions&output_format=json&actions=yes&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=allhosts&wato_folder=[folder]&_ack_notify=on&_down_comment=[comment]&_down_from_date=[start_date]&_down_from_time=[start_time]&_down_to_date=[end_date]&_down_to_time=[end_time]&_down_minutes=[down_minutes]&_down_duration=[down_duration]&_down_custom=Custom+time+range",
        svcregexp_downtime     => "[baseUrl]/view.py?filled_in=actions&output_format=json&actions=yes&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=allservices&service_regex=[svc_regexp]&_ack_notify=on&_down_comment=[comment]&_down_from_date=[start_date]&_down_from_time=[start_time]&_down_to_date=[end_date]&_down_to_time=[end_time]&_down_minutes=[down_minutes]&_down_duration=[down_duration]&_down_custom=Custom+time+range",
        host_downtime          => "[baseUrl]/view.py?output_format=json&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=host&host=[hostname]&_down_comment=[comment]&_down_from_date=[start_date]&_down_from_time=[start_time]&_down_to_date=[end_date]&_down_to_time=[end_time]&_down_minutes=[down_minutes]&_down_duration=[down_duration]&_down_custom=Custom+time+range",
        service_downtime       => "[baseUrl]/view.py?output_format=json&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=service&host=[hostname]&service=[service]&_down_comment=[comment]&_down_from_date=[start_date]&_down_from_time=[start_time]&_down_to_date=[end_date]&_down_to_time=[end_time]&_down_minutes=[down_minutes]&_down_duration=[down_duration]",
        del_folder_downtime    => "[baseUrl]/view.py?filled_in=actions&output_format=json&actions=yes&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=allhosts&wato_folder=[folder]&_down_remove=Remove+all&_remove_downtimes=Remove&downtime_id=[downtime_id]",
        del_host_downtime      => "[baseUrl]/view.py?output_format=json&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=api_downtimes&_remove_downtimes=Remove&downtime_id=[downtime_id]",
        del_service_downtime   => "[baseUrl]/view.py?output_format=json&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=api_downtimes&_remove_downtimes=Remove&downtime_id=[downtime_id]",
        del_svcregexp_downtime => "[baseUrl]/view.py?filled_in=actions&output_format=json&actions=yes&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=allservices&service_regex=[svc_regexp]&_down_remove=Remove+all&_remove_downtimes=Remove&downtime_id=[downtime_id]",
        all_downtimes          => "[baseUrl]/view.py?output_format=json&_transid=-1&_do_confirm=yes&_do_actions=yes&&_username=[user]&_secret=[password]&view_name=api_downtimes",
    },
    );

my %messages = (
    nagios => {
        success        => "Your command request was successfully submitted to Nagios for processing",
        not_authorized => "Sorry, but you are not authorized to commit the specified command.",
        no_author      => "Author was not entered",
    },
    multisite => {
        success        => "Successfully sent 1 commands",
        xt_success     => 'Successfully sent \d+ commands',
        not_authorized => "Invalid automation secret",
        no_author      => "TODO",
    }
);

# Load optional config file to override hardcoded defaults
my $config_file = "/usr/local/etc/conf.d/nagios_downtime.conf";
if(-f $config_file) {
    local $/=undef;
    open IN, $config_file;
    eval <IN>;
    close IN;
}

# Load command line options to override hardcoded defaults
Getopt::Long::Configure('bundling');
GetOptions(
    "h|help"         => \$help,
    "d|debug"        => \$debug,
    "i|interface=s"  => \$type,
    "m|mode=s"       => \$mode,
    "H|hostname=s"   => \$hostname,
    "S|server=s"     => \$server,
    "F|watofolder=s" => \$folder,
    "p|path=s"       => \$basePath,
    "u|user=s"       => \$user,
    "P|password=s"   => \$userPw,
    "t|downtime=i"   => \$downtimeDuration,
    "c|comment=s"    => \$downtimeComment,
    "s|service=s"    => \$service,
    "R|svcregexp=s"  => \$svcregexp,
    "a|start=s"      => \$start,
    "z|end=s"        => \$end,
    "T|timezone=s"   => \$timezone,
);

if($help) {
    about();
    exit(0);
}

if($type ne "nagios" and $type ne "multisite") {
    error("Invalid interface type specified.");
}

# Mode can be add or del, default is "add"
if($mode eq "") {
    $mode = "add";
}

if(($webProto eq "https" || $webPort eq "443") && !eval "require Crypt::SSLeay") {
    error("LWP can not handle SSL urls without Crypt::SLeay module.");
}

# Get hostname if not set via param
if($hostname eq "") {
    $hostname = hostname;
}

# When no nagios webserver is set the webserver and Nagios should be on the same
# host 
if($webServer eq "") {
    $webServer = $server;
}

# When a service name is set, this will be a service downtime
if($service ne "") {
    $downtimeType = SERVICE_DOWNTIME;
}

if ($folder ne "") {
    $downtimeType = FOLDER_DOWNTIME;
}

if ($svcregexp ne "") {
    $downtimeType = SVCREGEXP_DOWNTIME;
}

# Append the script internal downtime id when id storing is enabled
# The downtime ID is important to identify the just scheduled downtime for
# later removal. The CGIs do not provide the downtime id right after sending
# the schedule request. So it is important to tag the downtime with this.
if($storeDowntimeIds == 1) {
    $downtimeComment .= " (ID:".$downtimeId.")";
}

if ($downtimeDuration > 0) {
    # Calculate the end of the downtime
    {
        my $from = $now;
        $from = DateTime->new(
            %{ parse_datetime($start) },
            time_zone => $timezone,
            )->set_time_zone('UTC')->epoch()
            unless $start eq "";
        $end   = gettime($from+$downtimeDuration*60);
    }

    # Calculate the start of the downtime
    $start = gettime($now)
        if $start eq "";
} else {
    if ($start eq "") {
        error("Missing start date");
    }
    if ($end eq "") {
        error("Missing end date");
    }

    {
        my $dt_start = DateTime->new(
            %{ parse_datetime($start) },
            time_zone => $timezone,
            )->set_time_zone('UTC');
        my $dt_end   = DateTime->new(
            %{ parse_datetime($end) },
            time_zone => $timezone,
            )->set_time_zone('UTC');

        $downtimeDuration = $dt_end->delta_ms($dt_start)->in_units('minutes');
        $duration = sprintf("%02d:%02d", int($downtimeDuration)/60, $downtimeDuration%60);
        $downtimeDuration = 60 if $downtimeDuration > 60;
    }

    ($start_date, $start_time) = parse_datetime($start);
    ($end_date, $end_time) = parse_datetime($end);
}

#debug("now: $now, start: $start, end: $end, duration: $downtimeDuration");
 
# Check if web server is reachable via ping, if not, terminate the script
$p = Net::Ping->new();
if(!$p->ping($webServer)) {
    error("Given Nagios web server \"" . $webServer . "\" not reachable via ping.")
}

# Initialize the browser
$oBrowser = LWP::UserAgent->new(keep_alive => 1,timeout => 10);
$oBrowser->cookie_jar({});
$oBrowser->agent("nagios_downtime.pl / " . $version);

# Set the proxy address depending on the configured option
if($proxyAddress eq 'env') {
    $oBrowser->env_proxy(1);
} else {
    $oBrowser->env_proxy(0);
    $oBrowser->proxy([$webProto], $proxyAddress);
}
 
# Only try to auth if auth informations are given
if($authName ne "" && $userPw ne "") {
    # Set auth informations
    $oBrowser->credentials($webServer.':'.$webPort, $authName, $user => $userPw);
}

my $baseUrl = $webProto . "://" . $webServer . ":" . $webPort . $basePath;

# Handle the given action
switch($mode) {
    case 'add' {
        # Add a new scheduled downtime
        # ##########################################################################

        switch($downtimeType) {
            case HOST_DOWNTIME {
                # Schedule Host Downtime
                $url = get_url("host_downtime", {
                    baseUrl       => $baseUrl,
                    hostname      => $hostname,
                    user          => $user,
                    password      => $userPw,
                    comment       => $downtimeComment,
                    start_date    => $start_date,
                    start_time    => $start_time,
                    end_date      => $end_date,
                    end_time      => $end_time,
                    down_minutes  => $downtimeDuration,
                    down_duration => $duration,
                               });
            }
            case SERVICE_DOWNTIME {
                # Schedule Service Downtime
                $url = get_url("service_downtime", {
                    baseUrl       => $baseUrl,
                    hostname      => $hostname,
                    service       => $service,
                    user          => $user,
                    password      => $userPw,
                    comment       => $downtimeComment,
                    start_date    => $start_date,
                    start_time    => $start_time,
                    end_date      => $end_date,
                    end_time      => $end_time,
                    down_minutes  => $downtimeDuration,
                    down_duration => $duration,
                               });
            }
            case FOLDER_DOWNTIME {
                # Schedule Wato-Folder-based Downtime
                $url = get_url("folder_downtime", {
                    baseUrl       => $baseUrl,
                    hostname      => $hostname,
                    folder        => $folder,
                    user          => $user,
                    password      => $userPw,
                    comment       => $downtimeComment,
                    start_date    => $start_date,
                    start_time    => $start_time,
                    end_date      => $end_date,
                    end_time      => $end_time,
                    down_minutes  => $downtimeDuration,
                    down_duration => $duration,
                               });
            }
            case SVCREGEXP_DOWNTIME {
                # Schedule Service (regexp) Downtime
                $url = get_url("svcregexp_downtime", {
                    baseUrl       => $baseUrl,
                    hostname      => $hostname,
                    svc_regexp    => $svcregexp,
                    user          => $user,
                    password      => $userPw,
                    comment       => $downtimeComment,
                    start_date    => $start_date,
                    start_time    => $start_time,
                    end_date      => $end_date,
                    end_time      => $end_time,
                    down_minutes  => $downtimeDuration,
                    down_duration => $duration,
                               });
            }
        };

        debug("HTTP-GET: " . $url);

        # Send the get request to the web server
        $oResponse = $oBrowser->get($url);

        debug("HTTP-Response: " . $oResponse->content);

        # Handle response code, not in detail, only first char
        switch(substr($oResponse->code,0,1)) {
            # 2xx response code is OK
            case 2 {
                # Do some basic handling with the response content
                my $content = $oResponse->content;

                #print $dd->dump( $content );
                switch($content) {
                    case { index($content, get_msg("success")) != -1 } {
                        # Save the id of the just scheduled downtime
                        if($storeDowntimeIds == 1) {
                            saveDowntimeId();
                        }

                        success("Downtime was submitted successfully");
                    }
                    case { $content =~ get_msg("xt_success" ) } {
                        # Save the id of the just schedule downtime
                        if ($storeDowntimeIds == 1) {
                            saveDowntimeId();
                        }
                        success("Downtime was submitted successfully");
                    }
                    case { index($content, get_msg("not_authorized")) != -1 } {
                        error("Maybe not authorized or wrong host- or servicename.");
                    }
                    case { index($content, get_msg("no_author")) != -1 } {
                        error("No Author entered, define Author in \$user var.");
                    }
                    else {
                        error("Some undefined error occured, turn debug mode on to view what happened.");
                    }
                }
            }
            case 3 {
                error("HTTP Response code 3xx says \"moved url\" (".$oResponse->code.")");
            }
            case 4 {
                error("HTTP Response code 4xx says \"client error\" (".$oResponse->code.")\n"
                     ."Hint: This could be caused by wrong auth credentials and/or datetime settings in this script.");
            }
            case 5 {
                error("HTTP Response code 5xx says \"server error\" (".$oResponse->code.").");
            }
            else {
                error("HTTP Response code unhandled by script (".$oResponse->code.").");
            }
        }
    }
    case 'del' {
        # Delete the last scheduled downtime
        # ##########################################################################

        if($storeDowntimeIds != 1) {
            error("Unable to remove a downtime. The storingDowntimeIds option is set to disabled.");
        }

        # Read all internal downtime ids for this host/service
        my @downtimes = sort({ $b <=> $a } @{getDowntimeIds()});

        # Only proceed when downtimes found
        if (0 < scalar @downtimes) {
            # Get the nagios downtime id for the last scheduled downtime
            my $nagiosDowntimeId = getNagiosDowntimeId($downtimes[0]);

            debug("nagiosDowntimeId: $nagiosDowntimeId (". $downtimes[0] .")");
            if($nagiosDowntimeId ne "") {
                deleteDowntime($nagiosDowntimeId);
            }

            # Delete internal downtime id from downtime file
            # This only gets executed on successfull deleteDowntime() cause the
            # function terminates the script on any problem
            delDowntimeId($downtimes[0]);
        } else {
            error("Unable to remove a downtime. No previously scheduled downtime found.");
        }
    }
    else {
        error("Unknown mode was set (Available: add, del).");
    }
}

# Regular end of script
# ##############################################################################

# #############################################################
# Subs
# #############################################################

sub get_msg {
    my ($msg_key) = @_;
    return $messages{$type}{$msg_key};
}

sub get_url {
    my ($url_key, $map) = @_;
    my $this_url = $urls{$type}{$url_key};
    my $key;

    foreach $key (sort keys %$map) {
        my $tmpTmp = $map->{$key};
        $this_url =~ s/\[$key\]/$tmpTmp/g;
    }

    return $this_url;
}

sub error {
    my $msg = shift;
    print "ERROR: " . $msg . "\n";
    exit(1);
}

sub debug {
    if($debug) {
        my $msg = shift;
        print "Debug: " . $msg . "\n";
    }
}

sub success {
    my $msg = shift;
    print "OK: " . $msg . "\n";
    exit(0);
}

sub delDowntimeId {
    my $internalId = shift;

    my $file = $downtimePath.'/';
    switch($downtimeType) {
        case HOST_DOWNTIME {
            $file .= $hostname.'.txt';
        }
        case SERVICE_DOWNTIME {
            $file .= $hostname.'-'.$service.'.txt';
        }
        case FOLDER_DOWNTIME {
            $file .= 'wato.'.$folder.'.txt';
        }
        case SVCREGEXP_DOWNTIME {
            $file .= 'svcregexp.'.$svcregexp.'.txt';
        }
    }

    if(-f $file) {
        if(open(IN, "<", $file)) {
            my @contents = <IN>;
            close(IN);

            @contents = grep { !/^$internalId$/i } @contents;

            if (0 < scalar @contents) {
                if(open(OUT, ">", $file)) {
                    print OUT @contents;
                    close OUT;
                }
            }
        }
    }
}

sub getDowntimeIds {
    my @arr = ();

    my $file = $downtimePath.'/';
    if($downtimeType == 1) {
        $file .= $hostname.'.txt';
    } else {
        $file .= $hostname.'-'.$service.'.txt';
    }

    if(-f $file) {
        if(open(DAT, "<".$file)) {
            while(my $line = <DAT>) {
                # skip empty lines
                next if $line =~ m/^$/;

                # Do some validation
                if($line =~ m/[0-9]+/i) {
                    chomp($line);
                    push(@arr, $line);
                }
            }

            close(DAT);
        } else {
            error("Could not open temporary file (".$file.").");
        }
    }

    return \@arr;
}

sub saveDowntimeId {
    my $file = $downtimePath.'/';
    if($downtimeType == 1) {
        $file .= $hostname.'.txt';
    } else {
        $file .= $hostname.'-'.$service.'.txt';
    }

    if(open(DAT, ">>".$file)) {
        print DAT "\n$downtimeId";
        close(DAT);
    } else {
        error("Could not write downtime to temporary file (".$file.").");
    }
}

sub getNagiosDowntimeId {
    my $internalId = shift;

    # Get all downtimes
    my @aDowntimes = @{getAllDowntimes()};

    # Filter the just scheduled downtime
    for my $i ( 0 .. $#aDowntimes ) {
        # Matching by:
        #  - internal id in comment field
        #  - triggerId: N/A
        if($aDowntimes[$i]{'triggerId'} eq 'N/A' && $aDowntimes[$i]{'comment'} =~ m/\(ID:$internalId\)/) {

            debug("Found matching downtime (Host: ".$aDowntimes[$i]{'host'}.", "
                 ."Service: ".$aDowntimes[$i]{'service'}.", Entry-Time: ".$aDowntimes[$i]{'entryTime'}.", "
                 ."Downtime-Id: ".$aDowntimes[$i]{'downtimeId'}.")");
            return $aDowntimes[$i]{'downtimeId'};
        }
    }

    return "";
}

sub deleteDowntime {
    my $nagiosDowntimeId = shift;
    my $downtimeURL = undef;
    my $downtimeInfo = {
        baseUrl     => $baseUrl,
        user        => $user,
        password    => $userPw,
        downtime_id => $nagiosDowntimeId,
    };

    switch($downtimeType) {
        case HOST_DOWNTIME {
            # Host downtime
            $downtimeURL = 'del_host_downtime';
        }
        case SERVICE_DOWNTIME {
            $downtimeURL = 'del_service_downtime';
        }
        case FOLDER_DOWNTIME {
            $downtimeURL = 'del_folder_downtime';
            $downtimeInfo->{folder} = $folder;
        }
        case SVCREGEXP_DOWNTIME {
            $downtimeURL = 'del_svcregexp_downtime';
            $downtimeInfo->{svc_regexp} = $svcregexp;
        }
    }
    $url = get_url($downtimeURL, $downtimeInfo);

    my $oResponse;
    debug("downtimeURL: " . $downtimeURL);
    debug("HTTP-GET: " . $url);

    # Send the get request to the web server
    $oResponse = $oBrowser->get($url);

    debug("HTTP-Response: " . $oResponse->content);

    # Handle response code, not in detail, only first char
    switch(substr($oResponse->code,0,1)) {
        # 2xx response code is OK
        case 2 {
            # Do some basic handling with the response content
            switch($oResponse->content) {
                case { index($_[0], get_msg("success")) != -1 } {
                    print "OK: Downtime (ID: ".$nagiosDowntimeId.") has been deleted\n";
                }
                case { $_[0] =~ get_msg("xt_success") } {
                    print "OK: Downtime (ID: ".$nagiosDowntimeId.") has been deleted\n";
                }
                case { index($_[0], get_msg("not_authorized")) != -1 } {
                    error("Maybe not authorized or wrong host- or servicename.");
                }
                case { index($_[0], get_msg("no_author")) != -1 } {
                    error("No Author entered, define Author in \$user var.");
                }
                else {
                    error("Some undefined error occured, turn debug mode on to view what happened.");
                }
            }
        }
        case 3 {
            error("HTTP Response code 3xx says \"moved url\" (".$oResponse->code.").");
        }
        case 4 {
            error("HTTP Response code 4xx says \"client error\" (".$oResponse->code.")."
                 ."Hint: This could be caused by wrong auth credentials and/or datetime settings in this script.");
        }
        case 5 {
            error("HTTP Response code 5xx says \"server error\" (".$oResponse->code.").");
        }
        else {
            error("HTTP Response code unhandled by script (".$oResponse->code.").");
        }
    }
}

sub getAllDowntimes {
    my @arr = ();
    my $url;
    my $oResponse;

    # Url to downtime page
    $url = get_url("all_downtimes", {
        baseUrl    => $baseUrl,
        user       => $user,
        password   => $userPw,
    });

    debug("HTTP-GET: " . $url);

    # Fetch information via HTTP-GET
    $oResponse = $oBrowser->get($url);

    debug("HTTP-Response: " . $oResponse->content);

    # Parse all downtimes to an array
    if ($type eq 'nagios') {
        my $lineType = '';
        foreach my $line (split(/\n/, $oResponse->content)) {
            # Filter only downtime lines
            if($line =~ m/CLASS=\'downtime(Odd|Even)\'/) {
                my $lineType = 'downtime' . $1;
                if($line =~ m/<tr\sCLASS=\'$lineType\'><td\sCLASS=\'$lineType\'><A\sHREF=\'extinfo\.cgi\?type=1&host=([^\']+)\'>[^<]+<\/A><\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td>/i) {
                    debug("Found host downtime:\n"
                         ."Host: ".$1." EntryTime: ".$2." User: ".$3
                         ." Comment: ".$4." Start: ".$5." End: ".$6
                         ." Type: ".$7." Duration: ".$8." DowntimeID: ".$9
                         ." TriggerID: ".$10);

                    push @arr, { host => $1, service => '', entryTime => $2, user => $3, comment => $4, start => $5, end => $6, type => $7, duration => $8, downtimeId => $9, triggerId => $10 };
                } elsif($line =~ m/<tr\sCLASS=\'$lineType\'><td\sCLASS=\'$lineType\'><A\sHREF=\'extinfo\.cgi\?type=1&host=([^\']+)\'>[^<]+<\/A><\/td><td\sCLASS=\'$lineType\'><A\sHREF=\'extinfo\.cgi\?type=2&host=[^\']+&service=([^\']+)\'>[^<]+<\/A><\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td><td\sCLASS=\'$lineType\'>([^<]+)<\/td>/i) {

                    debug("Found service downtime:\n"
                         ."Host: ".$1." Service: ".$2." EntryTime: ".$3
                         ." User: ".$4." Comment: ".$5." Start: ".$6
                         ." End: ".$7." Type: ".$8." Duration: ".$9
                         ." DowntimeID: ".$10." TriggerID: ".$11);

                    push @arr, { host => $1, service => $2, entryTime => $3, user => $4, comment => $5, start => $6, end => $7, type => $8, duration => $9, downtimeId => $10, triggerId => $11 };
                }
            }
        }
    } elsif ($type eq 'multisite') {
        my @downtimes = @{decode_json($oResponse->content)};

        shift @downtimes; # delete header
        foreach my $row (@downtimes) {
            my @row = @{$row};

            push @arr, {
                host       => $row[0], #host,
                service    => $row[1], #service_description,
                entryTime  => $row[4], #downtime_entry_time,
                origin     => $row[2], #downtime_origin,
                user       => $row[3], #downtime_author,
                comment    => $row[10], #downtime_comment,
                start      => $row[5], #downtime_start_time,
                end        => $row[6], #downtime_end_time,
                type       => $row[7], #downtime_fixed,
                duration   => $row[8], #downtime_duration,
                recurring  => $row[9], #downtime_recurring,
                downtimeId => $row[11], #downtime_id,
                triggerId  => 'N/A',
            };
            if (scalar(@row) < 10) {
                error("The \"downtime_id\" field is missing in the \"downtime\" view. Please add it as last field.");
            }
        }
    }

    # return array of hashes
    return \@arr;
}

sub about {
        print <<'ABOUT';
Usage:
  nagios_downtime [-i <interface>] [-m add] [-H <hostname>] [-s <service>]
                  [-t <minutes>] [-S <webserver>] [-p <cgi-bin-path>]
                  [-u <username>] [-p <password>] [-d]
  nagios_downtime -m del [-i <interface>] [-H <hostname>] [-s <service>]
                  [-S <webserver>] [-p <cgi-bin-path>] [-u <username>]
                  [-p <password>] [-d]
  nagios_downtime -h

Nagios Downtime Script by Lars Michelsen <lars@vertical-visions.de>
Sends a HTTP(S) request to the nagios cgis to add a downtime for a host or 
service. Since version 0.5 the script can remove downtimes too when being
called in "del" mode.

Parameters:
 -i, --interface  Type of interface to be used to set downtimes (nagios or
                  multisite). Defaults to nagios. If OMD with Multisite or Check_MK is used set to multisite.
 
 -m, --mode       Mode to run the script in (Available: add, del)
 
 -H, --hostname   Name of the host the downtime should be scheduled for.
                  Important: The name must be same as in Nagios.
 -F, --watofolder Name of the Wato Folder the downtime should be scheduled for.
                  Important: Only valid in multisite context.
 -s, --service    Name of the service the downtime should be scheduled for.
                  Important: The name must be same as in Nagios. 
                  When empty or not set a host downtime is being submited.
 -R, --svcregexp  Regular expression for a service the downtime should be scheduled for.
 -t, --downtime   Duration of the fixed downtime in minutes
 -c, --comment    Comment for the downtime
 
 -S, --server     Nagios Webserver address (IP or DNS)
 -p, --path       Web path to API url. In case of nagios interface, the
                  path to the Nagios cgi-bin (Default: /nagios/cgi-bin).
                  In case of multisite API this path should point to
                  the base path of multisite (e.g. /check_mk or /omdsite/check_mk)
 -u, --user       Username to be used for accessing the API
 -P, --password   Password for accessing the API
 
 -d, --debug      Enable debug mode
 -h, --help       Show this message

If you call nagios_downtime without parameters the script takes the default options which are
hardcoded in the script.

ABOUT
}

sub gettime {
    my $timestamp;
    $timestamp = shift;

    if($timestamp eq "") {
            $timestamp = time;
    }

    my ($sec,$min,$hour,$mday,$month,$year,$wday,$yday,$isdst) = localtime($timestamp);
    # correct values
    $year += 1900;
    $month += 1;

    # add leading 0 to values lower than 10
    $month = $month < 10 ? $month = "0".$month : $month;
    $mday = $mday < 10 ? $mday = "0".$mday : $mday;
    $hour = $hour < 10 ? $hour = "0".$hour : $hour;
    $min = $min < 10 ? $min = "0".$min : $min;
    $sec = $sec < 10 ? $sec = "0".$sec : $sec;

    switch ($nagiosDateFormat) {
        case "euro" {
            return $mday."-".$month."-".$year." ".$hour.":".$min.":".$sec;
        }
        case "us" {
            return $month."-".$mday."-".$year." ".$hour.":".$min.":".$sec;
        }
        case "iso8601" {
            return $year."-".$month."-".$mday." ".$hour.":".$min.":".$sec;
        }
        case "strict-iso8601" {
            return $year."-".$month."-".$mday."T".$hour.":".$min.":".$sec;
        }
        else {
            error("No valid date format given in \$nagiosDateFormat");
        }
    }
}

sub parse_datetime {
    my ($datetime, $seconds) = @_;
    my ($date, $time) = split /T/, $datetime;
    my ($year, $month, $day) = split /-/, $date;
    my ($hour, $minute, $second) = split /:/, $time;

    return wantarray   ?
        defined $seconds && $seconds == 1 ? ($date, $time) : ($date, sprintf("%02d:%02d", $hour, $minute)) :
    {
        year => $year,
        month => $month,
        day => $day,
        hour => $hour,
        minute => $minute,
        second => $second,
    };
}

# #############################################################
# EOF
# #############################################################
